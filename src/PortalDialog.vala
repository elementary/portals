/*
 * SPDX-FileCopyrightText: 2026 elementary, Inc. (https://elementary.io)
 * SPDX-License-Identifier: LGPL-2.1-or-later
 */

public class PortalDialog : Gtk.Window, PantheonWayland.ExtendedBehavior {
    /**
     * The {@link PortalDialo.ResponseType} that has been selected by the user
     */
    public signal void response (ResponseType response);

    /**
     * The {@link GLib.Icon} that is used to display the primary_icon representing the app making the request
     */
    public GLib.Icon primary_icon { get; set; }

    /**
     * The {@link GLib.Icon} that is used to display a secondary_icon representing the action to be performed
     */
    public GLib.Icon secondary_icon { get; set; }

    /**
     * The secondary text, body of the dialog.
     */
    public string secondary_text { get; set; }

    /**
     * The child widget for the content area
     */
    public Gtk.Widget content { get; set; }

    /**
     * The label of the button which denies access
     */
    public string cancel_label { get; set; }

    /**
     * The label of the button which allows access
     */
    public string allow_label { get; set; }

    /**
     * Whether the allow button should be disabled
     */
    public bool form_valid { get; set; default = true; }

    public enum ResponseType {
        ALLOW,
        CANCEL
    }

    /**
     * The parent window identifier as described by https://flatpak.github.io/xdg-desktop-portal/docs/window-identifiers.html
     */
    public string parent_handle { get; set; }

    private Granite.Box button_box;

    construct {
        var primary_icon = new Gtk.Image.from_icon_name ("application-default-icon") {
            halign = START,
            icon_size = LARGE
        };

        var secondary_icon = new Gtk.Image.from_icon_name ("preferences-system-privacy") {
            halign = END,
            icon_size = LARGE
        };

        var overlay = new Gtk.Overlay () {
            child = secondary_icon,
            halign = CENTER
        };
        overlay.add_overlay (primary_icon);

        var header_label = new Granite.HeaderLabel ("") {
            size = H3
        };

        var header = new Granite.Box (VERTICAL);
        header.append (overlay);
        header.append (header_label);

        var cancel_button = new Gtk.Button.with_label (_("Don't Allow"));

        var allow_button = new Gtk.Button.with_label (_("Allow")) {
            receives_default = true
        };
        allow_button.add_css_class (Granite.CssClass.SUGGESTED);

        button_box = new Granite.Box (HORIZONTAL, HALF) {
            homogeneous = true
        };
        button_box.append (cancel_button);
        button_box.append (allow_button);

        var toolbarview = new Granite.ToolBox ();
        toolbarview.add_bottom_bar (button_box);
        toolbarview.add_top_bar (header);

        child = toolbarview;

        default_height = 425;
        default_width = 325;
        default_widget = allow_button;
        modal = true;

        // We need to hide the title area
        titlebar = new Gtk.Grid () {
            visible = false
        };

        add_css_class ("dialog");

        bind_property ("primary-icon", primary_icon, "gicon");
        bind_property ("secondary-icon", secondary_icon, "gicon");
        bind_property ("content", toolbarview, "content");
        bind_property ("title", header_label, "label");
        bind_property ("secondary-text", header_label, "secondary-text");
        bind_property ("form-valid", allow_button, "sensitive");
        bind_property ("allow-label", allow_button, "label");
        bind_property ("cancel-label", cancel_button, "label");

        ((Gtk.Widget) this).realize.connect (on_realize);

        allow_button.clicked.connect (() => response (ResponseType.ALLOW));
        cancel_button.clicked.connect (() => response (ResponseType.CANCEL));
    }

    private void on_realize () {
        unowned var toplevel = (Gdk.Toplevel) get_surface ();

        if (parent_handle != "") {
            try {
                ExternalWindow.from_handle (parent_handle).set_parent_of (toplevel);
            } catch (Error e) {
                warning ("Failed to associate portal window with parent '%s': %s", parent_handle, e.message);
                make_sticky ();
            }
        } else {
            make_sticky ();
        }
    }

    private void make_sticky () {
        child.realize.connect (() => {
            connect_to_shell ();
            make_centered ();
            set_keep_above ();
        });
    }
}
